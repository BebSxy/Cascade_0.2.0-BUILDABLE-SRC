buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven {
			name = "forge"
			url = "https://files.minecraftforge.net/maven"
		}
		maven {
			name = "sponge"
			url = "https://repo.spongepowered.org/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
		classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "org.spongepowered.mixin"

group = "cascade"
version = "0.2.0"
archivesBaseName = "cascade"

minecraft {
	version = "1.12.2-14.23.5.2847"
	runDir = "run"
	mappings = "stable_39"
	coreMod = "cascade.mixin.MixinLoader"
	makeObfSourceJar = false
}

repositories {
	maven {
		name = "spongepowered-repo"
		url  = "https://repo.spongepowered.org/repository/maven-public/"
	}
	maven {
		name = "jitpack.io"
		url  = "https://jitpack.io"
	}
	jcenter()
	mavenCentral()
}

dependencies {
	compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
		exclude module: "launchwrapper"
		exclude module: "commons-io"
		exclude module: "guava"
		exclude module: "gson"
	}

	compile group: "com.googlecode.json-simple", name: "json-simple", version: "1.1.1"
	compile "club.minnced:java-discord-rpc:2.0.2"
}

shadowJar {
	dependencies {
		include(dependency("org.spongepowered:mixin"))
		include(dependency('com.googlecode.json-simple:json-simple:1.1.1'))
		include(dependency('club.minnced:java-discord-rpc:2.0.2'))
	}

	exclude 'dummyThing'
	exclude 'LICENSE.txt'
	classifier = 'release'
}

mixin {
	defaultObfuscationEnv searge
	add sourceSets.main, "mixins.cascade.refmap.json"
}

reobf {
	shadowJar {
		mappingType = 'SEARGE'
		classpath   = sourceSets.main.compileClasspath
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"

		expand "version": project.version, "mcversion": project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}

	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {
		attributes (
				'MixinConfigs': 'mixins.cascade.json',
				'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
				'TweakOrder': 0,
				'FMLCorePluginContainsFMLMod': 'true',
				'FMLCorePlugin': 'cascade.mixin.MixinLoader',
				'ForceLoadAsMod': 'true',
				'FMLAT': 'cascade_at.cfg'
		)
	}
}

build.dependsOn(shadowJar)